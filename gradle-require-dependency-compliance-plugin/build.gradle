plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.18.0'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.9'
}

repositories {
    mavenCentral()
}

// read all scripts from ./gradle.d
file('gradle.d').listFiles().sort().each {
    if (it =~ /.*\.gradle$/) {
        apply from: relativePath(it)
    }
}

group 'de.acetous'
version = "3.0.0-SNAPSHOT"

gradlePlugin {
    plugins {
        dependencyCompliance {
            id = 'de.acetous.dependency-compliance'
            implementationClass = 'de.acetous.dependencycompliance.DependencyCompliancePlugin'
            displayName = 'Gradle Dependency Compliance Plugin'
            description = 'A Gradle plugin to check and track dependencies in your project. Useful for meeting compliance targets, including 3rd-party libraries.'
        }
    }
}

pluginBundle {
    website = 'https://github.com/acetous/gradle-require-dependency-compliance'
    vcsUrl = 'https://github.com/acetous/gradle-require-dependency-compliance.git'
    tags = ['dependencies', 'compliance']
}

test {
    if (project.hasProperty("gradleVersion")) {
        systemProperties["gradleVersion"] = project.property("gradleVersion")
    }
}

dependencies {
    implementation('com.google.code.gson:gson:2.9.0')

    testImplementation('junit:junit:4.13.2')
    testImplementation('commons-io:commons-io:2.11.0')
    testImplementation('org.assertj:assertj-core:3.22.0')
    testImplementation('com.google.guava:guava:31.0.1-jre')
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

project.tasks["sonarqube"].dependsOn "jacocoTestReport"
